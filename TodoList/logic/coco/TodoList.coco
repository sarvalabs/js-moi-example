// A simple logic with an owner and list of todos
// where todo's can be added, retrieved and marked as completed
// only by the onwer who deploy's the logic

////////////////////////
// Module Name
///////////////////////
coco TodoList

////////////////////////
// Persisitent State
///////////////////////

// The persistant state is used to define stateful
// values managed intrinsically by the logic
// State variables can be also be accessed by using
// their variable names as endpoints
state persistent:
    todos []Todo
    owner Address

////////////////////////
// Custom Classes
///////////////////////

// The class Todo is a structure for defining a Todo datatype,
// where it can possess multiple fields and methods
class Todo:
    field id U64
    field name String
    field completed Bool

////////////////////////
// Deploy Endpoints
///////////////////////

// InitOwner!() deployer is a constructor that assigns a 
// value to the state variable owner
endpoint deployer InitOwner!():
    mutate Address(Sender) -> TodoList.State.owner

////////////////////////
// Internal Functions 
///////////////////////

// Function to check if the caller is the owner of the Logic
func onlyOwner():
    observe owner <- TodoList.State.owner:
        if(Address(Sender) != owner):
            throw "Not an Owner"

// func's can only be called internally within the logic

////////////////////////
// Mutate/Write Endpoints
///////////////////////

// Add!() is an invokable that adds the given Todo to
// the list of  Todo's
endpoint invokable AddTodo!(name String)->(addedTodo Todo):
    onlyOwner()

    mutate todos <- TodoList.State.todos:
        var newTodo = Todo{id: len(todos), name: name, completed: false}
        todos = append(todos, newTodo)
        yield addedTodo newTodo

// MarkCompleted!() is an invokable that marks the given
// todo as completed
endpoint invokable MarkTodoCompleted!(id U64):
    onlyOwner()

    mutate todos <- TodoList.State.todos:
            todos[id].completed = true

// You might have noticed
// We used '!' after the endpoint name
// That is to indicate that this 
// endpoint mutates the logic's state.
// Hence we call them write endpoints

////////////////////////
// Observe/Read Endpoints
///////////////////////

// GetTodos() is an invokable that gets all
// the stored todos
endpoint invokable GetTodos() -> (todos []Todo):
    observe todos <- TodoList.State.todos


